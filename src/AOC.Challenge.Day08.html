<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"><html xmlns="http://www.w3.org/1999/xhtml"><head><link rel="stylesheet" type="text/css" href="style.css" /><script type="text/javascript" src="highlight.js"></script></head><body><pre><span class="">{-# LANGUAGE QuasiQuotes #-}

-- |
-- Module      : AOC.Challenge.Day08
-- License     : BSD3
--
-- Stability   : experimental
-- Portability : non-portable
--
-- Day 8.  See &quot;AOC.Solver&quot; for the types used in this module!
--
-- After completing the challenge, it is recommended to:

module AOC.Challenge.Day08 (
    day08a
  , day08b
  ) where

import           AOC.Common      (Point, parseAsciiMap, countTrue)
import           AOC.Solver      ((:~&gt;)(..), dyno_)
import           Control.Monad   (guard)
import           Data.Bifunctor  (second)
import           Data.List       (unfoldr, transpose)
import           Data.List.Split (chunksOf)
import           Data.Map        (Map)
import           Data.Maybe      (listToMaybe, mapMaybe)
import           Data.Ord        (comparing)
import           Data.Semigroup  (Min(..))
import           Data.Set        (Set)
import           Linear          (V2(..))
import           Safe            (minimumByMay)
import           Text.Heredoc    (here)
import qualified Data.Map        as M
import qualified Data.Set        as S

day08a :: String :~&gt; Int
day08a = MkSol
    { sParse = Just
    , sShow  = show
    , sSolve = fmap answer
             . minimumByMay (comparing (countTrue (== '0')))
             . chunksOf (dyno_ &quot;w&quot; 25 * dyno_ &quot;h&quot; 6)
    }
  where
    answer x  = countTrue (== '1') x * countTrue (== '2') x

day08b :: [String] :~&gt; String
day08b = MkSol
    { sParse = Just . chunksOf 150
    -- , sShow  = unlines . chunksOf 25 . map (\case '0' -&gt; ' '; _ -&gt; '#')
    , sShow  = mapMaybe (`M.lookup` letterMap)
             . unfoldr peel
             . M.keysSet
             . parseAsciiMap (guard . (== '1'))
             . unlines
             . chunksOf 25
    , sSolve = traverse (listToMaybe . filter (/= '2')) . transpose
    }

peel :: Set Point -&gt; Maybe (Set Point, Set Point)
peel ps = do
    Min xMin &lt;- flip foldMap ps $ \(V2 x _) -&gt; Just (Min x)
    let ps' = subtract (V2 xMin 0) `S.map` ps
    pure $ S.partition (\(V2 x _) -&gt; x &lt; 4) ps'

-- | A map of a set of &quot;on&quot; points (for a 4x6 grid) to the letter they
-- represent
letterMap :: Map (Set Point) Char
letterMap = M.fromList
          . uncurry (zipWith (flip (,)))
          . second (unfoldr peel . M.keysSet . parseAsciiMap (guard . (== '#')))
          $ rawLetterforms

rawLetterforms :: (String, String)
rawLetterforms = (&quot;ABCEFGHJKLPRUYZ&quot;, drop 1 [here|
.##.###..##.########.##.#..#..###..##...###.###.#..##...#####
</span><span class="hs-cpp">#..##..##..##...#...#..##..#...##.#.#...#..##..##..##...#...#
#..####.#...###.###.#...####...###..#...#..##..##..#.#.#...#.
#####..##...#...#...#.###..#...##.#.#...###.###.#..#..#...#..
#..##..##..##...#...#..##..##..##.#.#...#...#.#.#..#..#..#...
#..####..##.#####....####..#.##.#..######...#..#.##...#..####
</span><span class="hs-special">|]</span><span class="hs-special">)</span><span>
</span><a name="line-83"></a></pre></body></html>